/*
 * Copyright (c) 2016,  nwillc@gmail.com
 *
 * Permission to use, copy, modify, and/or distribute this software for any
 * purpose with or without fee is hereby granted, provided that the above
 * copyright notice and this permission notice appear in all copies.
 *
 * THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
 * WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
 * MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
 * ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
 * WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
 * ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
 * OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
 */

buildscript {
    repositories {
        mavenLocal()
        jcenter {
            url 'http://jcenter.bintray.com'
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url "https://maven.eveoh.nl/content/repositories/releases"
        }
    }
    dependencies {
        classpath "gradle.plugin.com.github.nwillc:vplugin:1.2.5",
                "nl.eveoh:gradle-aspectj:1.6",
                "gradle.plugin.com.github.nwillc:buildinfo:0.0.4",
                'au.com.ish.gradle:release:2.2.2',
                'org.tmatesoft.sqljet:sqljet:1.1.9'
    }
}

project.ext {
    aspectjVersion = "1.8.8"
}

apply plugin: "com.github.nwillc.buildinfo"
apply plugin: "com.github.nwillc.vplugin"
apply plugin: 'java'
apply plugin: 'release'
apply plugin: "aspectj"
apply plugin: 'groovy'


repositories {
    mavenLocal()
    jcenter {
        url 'http://jcenter.bintray.com'
    }
    maven {
        url "http://dl.bintray.com/nwillc/maven"
    }
}

configurations {
    compile.exclude group: 'org.slf4j', module: 'slf4j-simple'
}

dependencies {
    aspectpath 'com.github.nwillc:cache-annotations:0.1.3'
    compile 'org.slf4j:slf4j-jdk14:1.7.13',
            "org.aspectj:aspectjrt:1.8.8",
            'com.sparkjava:spark-core:2.3',
            'commons-cli:commons-cli:1.3',
            'io.orchestrate:orchestrate-client:0.12.1',
            'com.google.inject:guice:4.0',
            'org.apache.httpcomponents:httpclient:4.5.1',
            'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.7.0',
            'javax.cache:cache-api:1.0.0',
            'com.github.nwillc:cache-annotations:0.1.3',
            'com.github.nwillc:simplecache:1.0.3',
            'com.google.api-client:google-api-client:1.21.0'
    testCompile 'junit:junit:4.12',
            'com.github.nwillc:jdk_contract_tests:1.7.3',
            'org.mockito:mockito-core:2.0.40-beta',
            'org.assertj:assertj-core:3.3.0'
}

group = "com.github.nwillc"
version = "1.0.2"
targetCompatibility = "1.8"
sourceCompatibility = "1.8"

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked"
    }
}

test {
    useJUnit {
        excludeCategories 'com.github.nwillc.mysnipserver.IntegrationTest'
    }

    beforeTest { descriptor ->
        logger.lifecycle("\tRunning " + descriptor.className + "." + descriptor.name)
    }

    afterSuite { descriptor, result ->
        if (descriptor.parent == null) {
            logger.lifecycle("\tTests run: " + result.testCount
                    + ", Failures: " + result.failedTestCount
                    + ", Skipped: " + result.skippedTestCount)
        }
    }

    testLogging {
        exceptionFormat "full"
    }
}

task integrationTest(type: Test) {
    useJUnit {
        includeCategories 'com.github.nwillc.mysnipserver.IntegrationTest'
    }
}

task stage(type: Copy) {
    from sourceSets.main.runtimeClasspath
    into 'build/staging'
}

jar {
    exclude ( "public/tests" )  // Exclude js unit tests
}

stage.dependsOn build
processResources.dependsOn buildInfo

release {
    failOnSnapshotDependencies = true
    allowLocalModifications = true
    releaseDryRun = false
    scm = 'git'
}
