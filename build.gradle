/*
 * Copyright (c) 2017, nwillc@gmail.com
 *
 * Permission to use, copy, modify, and/or distribute this software for any
 * purpose with or without fee is hereby granted, provided that the above
 * copyright notice and this permission notice appear in all copies.
 *
 * THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
 * WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
 * MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
 * ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
 * WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
 * ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
 * OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
 */

buildscript {
    repositories {
        mavenLocal()
        maven {
            url mvn_repository
        }
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'gradle.plugin.com.github.nwillc:buildinfo:0.1.4',
                'com.bmuschko:gradle-docker-plugin:3.2.0',
                'gradle.plugin.com.github.nwillc:vplugin:2.1.1',
                'com.github.jengelman.gradle.plugins:shadow:2.0.1',
                'com.bmuschko:gradle-docker-plugin:3.2.0'
    }
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'maven'
apply plugin: 'jacoco'
apply plugin: 'pmd'
apply plugin: 'com.github.nwillc.vplugin'
apply plugin: 'com.bmuschko.docker-remote-api'
apply plugin: 'com.github.nwillc.buildinfo'
apply plugin: 'com.github.johnrengelman.shadow'

import com.bmuschko.gradle.docker.tasks.container.DockerCreateContainer
import com.bmuschko.gradle.docker.tasks.container.DockerStartContainer
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage
import com.bmuschko.gradle.docker.tasks.image.Dockerfile

group = 'com.github.nwillc'
version = '2.9.3'
targetCompatibility = '1.8'
sourceCompatibility = '1.8'
mainClassName = 'com.github.nwillc.mysnipserver.RatPackApp'

repositories {
    mavenLocal()
    maven {
        url mvn_repository
    }
}

dependencies {
    compile "com.github.nwillc:opa:$opa_version",
            "io.ratpack:ratpack-core:$ratpack_version",
            "io.ratpack:ratpack-exec:$ratpack_version",
            "io.ratpack:ratpack-session:$ratpack_version",
            "io.ratpack:ratpack-guice:$ratpack_version",
            "net.sf.jopt-simple:jopt-simple:$jopt_simple_version",
            "com.fasterxml.jackson.core:jackson-core:$jackson_fasterxml_version",
            "com.fasterxml.jackson.core:jackson-databind:$jackson_fasterxml_version",
            "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:$jackson_fasterxml_version",
            "com.fasterxml.jackson.core:jackson-annotations:$jackson_fasterxml_version",
            "com.google.http-client:google-http-client-jackson2:$google_http_client",
            "com.google.http-client:google-http-client:$google_http_client",
            'com.google.inject:guice:4.1.0',
            'com.groupon:locality-uuid:1.1.1',
            "javax.validation:validation-api:$javax_validation_version",
            "org.mongodb:mongo-java-driver:$mongo_driver_version",
            "org.tinylog:tinylog:$tiny_log_version",
            "com.graphql-java:graphql-java-annotations:$graphql_annotations_version",
            "com.h2database:h2:$h2database_version",
            'org.apache.commons:commons-dbcp2:2.1.1'

    compile("com.google.api-client:google-api-client:$google_http_client") {
        exclude group: 'com.google.guava', module: 'guava-jdk5'
    }

    runtime "org.tinylog:slf4j-binding:$slf4j_version"

    testCompile "org.jmockit:jmockit:$jmockit_version",
            "com.github.fakemongo:fongo:$fakemongo_version",
            "org.assertj:assertj-core:$assertj_version",
            "com.github.nwillc:jdk_contract_tests:$jdk_contract_tests_version",
            "junit:junit:4.12"
}

configurations {
    compile.exclude group: 'com.google.code.findbugs', module: 'jsr305'
}

compileTestJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.compilerArgs += '-parameters'
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
        csv.enabled = false
    }
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs += ['-Xlint:unchecked']
    }
}

processResources.dependsOn buildInfo

shadowJar {
    baseName = "$project.name-$project.version-standalone"
    classifier = null
    version = null
}

pmd {
    toolVersion = '4.3'
    sourceSets = [sourceSets.main]
    ignoreFailures = true
    ruleSets = [
            'basic',
            'braces',
            'naming',
            'clone',
            'codesize',
            'controversial',
            'design',
            'finalizers',
            'imports',
            'junit',
            'logging-java',
            'migrating',
            'optimizations',
            'strictexception',
            'strings',
            'sunsecure',
            'typeresolution',
            'unusedcode'
    ]
}

test {
    beforeTest { descriptor ->
        logger.lifecycle("\tRunning $descriptor.className.$descriptor.name")
    }

    afterSuite { descriptor, result ->
        if (descriptor.parent == null) {
            logger.lifecycle("\tTests run: $result.testCount, Failures: $result.failedTestCount, Skipped: $result.skippedTestCount")
        }
    }

    testLogging {
        showStandardStreams true
        exceptionFormat "full"
    }
}

run {
    if (project.hasProperty("appArgs")) {
        args Eval.me(appArgs)
    }
}

docker {
    registryCredentials {
        url = 'https://index.docker.io/v1/'
        username = 'nwillc'
        password = System.getenv('DOCKER_PASSWD')
        email = my_email_address
    }
}

task createDockerfile(type: Dockerfile) {
    def labels = [maintainer: my_email_address,
                  version: project.version]

    destFile = project.file('build/docker/Dockerfile')
    from 'anapsix/alpine-java:8_server-jre_unlimited'
    label labels
    exposePort mysnipserver_port.toInteger()
    runCommand 'mkdir -p /opt/service/db'
    volume '/opt/service/db'
    copyFile "libs/${shadowJar.baseName}.jar", '/opt/service'
    workingDir '/opt/service'
    defaultCommand '--port', mysnipserver_port, '--store', 'H2'
    entryPoint 'java', '-Djava.awt.headless=true', '-jar', "${shadowJar.baseName}.jar"
}

task buildImage(type: DockerBuildImage, dependsOn: [assemble, createDockerfile]) {
    inputDir = project.file("build")
    dockerFile = createDockerfile.destFile
    tag = "nwillc/$project.name:$project.version"
}

task pushImage(type: DockerPushImage, dependsOn: buildImage) {
    imageName buildImage.tag
}

task createContainer(type: DockerCreateContainer, dependsOn: buildImage) {
    targetImageId { buildImage.getImageId() }
    portBindings = ["$mysnipserver_port:$mysnipserver_port"]
    containerName = project.name
}

task startContainer(type: DockerStartContainer, dependsOn: createContainer) {
    targetContainerId { createContainer.getContainerId() }
}