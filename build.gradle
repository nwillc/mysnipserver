/*
 * Copyright (c) 2016,  nwillc@gmail.com
 *
 * Permission to use, copy, modify, and/or distribute this software for any
 * purpose with or without fee is hereby granted, provided that the above
 * copyright notice and this permission notice appear in all copies.
 *
 * THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
 * WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
 * MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
 * ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
 * WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
 * ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
 * OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
 */

buildscript {
    repositories {
        mavenLocal()
        jcenter {
            url 'http://jcenter.bintray.com'
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url "https://maven.eveoh.nl/content/repositories/releases"
        }
    }
    dependencies {
        classpath "gradle.plugin.com.github.nwillc:vplugin:1.2.5",
                "gradle.plugin.com.github.nwillc:buildinfo:0.1.2",
                'au.com.ish.gradle:release:2.2.2',
                'org.tmatesoft.sqljet:sqljet:1.1.9'
    }
}

apply plugin: "com.github.nwillc.buildinfo"
apply plugin: "com.github.nwillc.vplugin"
apply plugin: 'java'
apply plugin: 'release'
apply plugin: 'groovy'


repositories {
    mavenLocal()
    jcenter {
        url 'http://jcenter.bintray.com'
    }
    maven {
        url "http://dl.bintray.com/nwillc/maven"
    }
}

configurations {
    compile.exclude group: 'org.slf4j', module: 'slf4j-simple'
}

dependencies {
    compile 'com.sparkjava:spark-core:2.3',
            'commons-cli:commons-cli:1.3',
            'io.orchestrate:orchestrate-client:0.13.0',
            'com.google.inject:guice:4.0',
            'org.apache.httpcomponents:httpclient:4.5.2',
            'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.8.0.rc1',
            'javax.cache:cache-api:1.0.0',
            'com.google.api-client:google-api-client:1.22.0',
            'com.google.http-client:google-http-client:1.22.0',
            'com.github.ben-manes.caffeine:caffeine:2.3.0',
            'com.github.ben-manes.caffeine:jcache:2.3.0',
            'org.tinylog:tinylog:1.1'
    runtime 'org.tinylog:slf4j-binding:1.1'
    testCompile 'junit:junit:4.12',
            'com.github.nwillc:jdk_contract_tests:1.7.5',
            'org.mockito:mockito-core:2.0.54-beta',
            'org.assertj:assertj-core:3.4.1'
}

group = "com.github.nwillc"
version = "1.1.3"
targetCompatibility = "1.8"
sourceCompatibility = "1.8"

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked"
    }
}

test {
    useJUnit {
        excludeCategories 'com.github.nwillc.mysnipserver.IntegrationTest'
    }

    beforeTest { descriptor ->
        logger.lifecycle("\tRunning " + descriptor.className + "." + descriptor.name)
    }

    afterSuite { descriptor, result ->
        if (descriptor.parent == null) {
            logger.lifecycle("\tTests run: " + result.testCount
                    + ", Failures: " + result.failedTestCount
                    + ", Skipped: " + result.skippedTestCount)
        }
    }

    testLogging {
        exceptionFormat "full"
    }
}

task integrationTest(type: Test) {
    useJUnit {
        includeCategories 'com.github.nwillc.mysnipserver.IntegrationTest'
    }
}

task stage(type: Copy) {
    from sourceSets.main.runtimeClasspath
    into 'build/staging'
}

stage.dependsOn build
processResources.dependsOn buildInfo

release {
    failOnSnapshotDependencies = true
    allowLocalModifications = true
    releaseDryRun = false
    scm = 'git'
}
